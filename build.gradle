plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
    id 'org.cyclonedx.bom' version '1.8.2'
    //id 'org.graalvm.buildtools.native' version '0.10.2'
}

group = 'com.thomasvitale'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '21'

configurations.all {
    resolutionStrategy {
        failOnNonReproducibleResolution()
    }
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('jar') {
	enabled = false
}

tasks.named('bootBuildImage') {
    if (System.getProperty( "os.arch" ).toLowerCase().startsWith('aarch')) {
        builder = "paketobuildpacks/builder-jammy-buildpackless-tiny"
        buildpacks = [ "gcr.io/paketo-buildpacks/java" ]
    } else {
        builder = "docker.io/paketobuildpacks/builder-jammy-tiny"
    }

    imageName = "${project.name}"
}

tasks.named('cyclonedxBom') {
    schemaVersion = "1.5"
    includeConfigs = ["runtimeClasspath"]
}
